NAME main
#include "msp430x14x.h" ; Стандартные определения
result EQU 0200h ; Начало таблицы
PUBLIC main
ORG 0FFFEh
DC16 main
ORG 0FFEEh ; Прерывание от ADC12
DW ADC12ISR
RSEG CODE
main mov #0A00h,SP ; Инициализация стека
mov #WDTPW+WDTHOLD,&WDTCTL ; Стоп WATCHDOG
mov #ADC12ON+REFON+REF2_5V+SHT0_6,&ADC12CTL0
; Включение АЦП, установка опорного напряжения 2.5В, установка режима УВХ
mov #SHP,&ADC12CTL1 ; Режим пуска
mov.b #INCH_0+SREF_1,&ADC12MCTL0 ; Выбор канала
bis #BIT0,&ADC12IE ; Разрешить прерывание
mov #03600h,R15 ; Задержка для завершения
L$1 dec R15 ; переходных процессов
jnz L$1
bis #ENC,&ADC12CTL0 ; Запуск преобразования
clr R6
eint ; Разрешение прерываний
Moop bis #ADC12SC,&ADC12CTL0 ; Пуск преобразования
bis #CPUOFF,SR ; Переход в режим малого
; потребления для ожидания
; завершения преобразования
jmp Moop ; Организация цикла.
;------------------------------------------------------------------------------
ADC12ISR ; Подпрограмма обработки прерывания для ADC12
;------------------------------------------------------------------------------
mov &ADC12MEM0,result(R6) ; Сохранить результат
incd R6 ; Инкремент указателя
and #0FEh,R6 ; Модификация буфера
bic #CPUOFF,0(SP) ; Возврат в активный режим
reti
END main